plugins {
    id 'org.springframework.boot' version '3.5.0' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'java'
    id 'com.diffplug.spotless' version '6.25.0' apply false
}

wrapper {
    gradleVersion = '8.14.1'
    distributionType = Wrapper.DistributionType.ALL
}

subprojects {
    apply plugin: 'java'
    if (project.name != 'common-dto') {
        apply plugin: 'org.springframework.boot'
        apply plugin: 'io.spring.dependency-management'
    }
    apply plugin: 'com.diffplug.spotless'

    group = 'com.example.hotel'
    version = '0.0.1-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        implementation 'org.mapstruct:mapstruct:1.5.5.Final'
        annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
        if (project.name != 'common-dto') {
            testImplementation 'org.springframework.boot:spring-boot-starter-test'
            implementation 'io.github.resilience4j:resilience4j-spring-boot3:2.2.0'
        }
    }

    spotless {
        java {
            googleJavaFormat('1.17.0')
            target 'src/**/*.java'
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}
